plugins {
    id 'org.jetbrains.kotlin.plugin.compose' version '2.2.20' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.2.20'
}

allprojects {
    repositories {
        // 使用阿里云镜像
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        maven { url 'https://maven.aliyun.com/repository/public' }
        
        // 备用镜像
        maven { url 'https://repo1.maven.org/maven2/' }
        
        // 原始仓库作为后备
        google()
        mavenCentral()
    }
}

// https://github.com/flutter/flutter/issues/153281#issuecomment-2292201697
rootProject.buildDir = '../build'
subprojects {
    afterEvaluate { project ->
        if (project.extensions.findByName("android") != null) {
            Integer pluginCompileSdk = project.android.compileSdk
            if (pluginCompileSdk != null && pluginCompileSdk < 31) {
                project.logger.error(
                        "Warning: Overriding compileSdk version in Flutter plugin: "
                                + project.name
                                + " from "
                                + pluginCompileSdk
                                + " to 31 (to work around https://issuetracker.google.com/issues/199180389)."
                                + "\nIf there is not a new version of " + project.name + ", consider filing an issue against "
                                + project.name
                                + " to increase their compileSdk to the latest (otherwise try updating to the latest version)."
                )
                project.android {
                    compileSdk 31
                }
            }
        }
    }
    
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    project.evaluationDependsOn(":app")
}
tasks.register("clean", Delete) {
    delete rootProject.buildDir
}